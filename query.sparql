PREFIX ncit:<http://ncicb.nci.nih.gov/xml/owl/EVS/Thesaurus.owl#>
PREFIX roo: <http://www.cancerdata.org/roo/>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>

PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
SELECT ?patient ?age ?sex ?cT ?cN ?survival 
WHERE { 
	?patient a ncit:C16960 ;
     		roo:P100008 ?diseaseObj ;
      		roo:P100311 ?survivalObj ;
    		roo:P100018 ?sexObj ;
       		roo:P100000 ?ageObj. 
    
    #Age
	?ageObj roo:P100042 ?age.    

    #Survival
	?survivalObj a ?survival.
    ?survival rdfs:subClassOf roo:C100014.
    FILTER(?survival!=roo:C100014 && !ISBLANK(?survival)).
        
    #Sex
	{ ?sexObj rdf:type ?sex.
    ?sex rdfs:subClassOf ncit:C28421.
    #FILTER(?sex!=ncit:C28421 && !ISBLANK(?sex)).
    VALUES (?sex ?sexNumeric) {
        (ncit:C20197 "1"^^xsd:int )
        (ncit:C16576 "0"^^xsd:int )
        } }
    
    
    #Stage
    ?diseaseObj 	roo:P100025 ?cTObj ;
             	roo:P100025 ?cNObj.
    
    #Tstage
    ?cTObj a ncit:C48885 ; 
        	a ?cT.
    ?cT rdfs:subClassOf ncit:C48885.
    FILTER(?cT!=ncit:C48885 && !ISBLANK(?cT)).
    
    #Nstage
    ?cNObj 	a ncit:C48884 ;
        	a ?cN.
    ?cN rdfs:subClassOf ncit:C48884.
    FILTER(?cN!=ncit:C48884 && !ISBLANK(?cN)).

} 
LIMIT 1000
